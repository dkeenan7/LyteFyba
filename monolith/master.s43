;
; This is similar to the ACCEPT (slave) function, except that it doesn't interpret commands.
; Instead it injects Z, G, i (etc) commands as needed, and in synch with the incoming stream
; (i.e. it waits for either a CR or a timeout before sending).
;
; This represents the BMU behaviour as the ID = 0 device, the beginning of the comms chain,
; and hence the Master, whereas ACCEPT represents its behaviour as the ID = 255 device,
; the end of the comms chain.

Master:
		cmp.b	#$0D,R8				; Carriage return?
		_IF		_EQ
			; No longer blocked, and if we unblocked via a timeout but didn't have anything to inject,
			;	then forget about the timeout (CR no longer required for next injection)
			bic.b	#bBlocked | bTimeout,&masterFlags
		_ELSE
			bit.b	#bBlocked,&masterFlags
			_IF		_Z						; Presently not blocked. A non null non linefeed will block
				_COND
					tst.b	R8				; Not null? (Indicates no char received)
				_AND_IF		_NZ
					cmp.b	#$0A,R8			; And not linefeed?
				_AND_IF	_NE
					bis.b	#bBlocked,&masterFlags	; We're now blocked
					mov.b	&ticks,R9				; Get the medium speed counter
					add.b	#12*StatusFreq,R9		; Get twelve seconds after that
					mov.b	R9,&masterUnblockTicks	; Save that time
				_ENDIFS
			_ELSE						; Presently blocked. A carriage return will unblock
				; Presently blocked. Check for timeout, with care for wrapping
				cmp.b		&masterUnblockTicks,&ticks
				_IF _NN						; If ticks minus masterUnblockTicks is not negative
					bic.b	#bBlocked,&masterFlags ; then unblock anyway
					bis.b	#bTimeout,&masterFlags ; remember we forced an unblock
				_ENDIF
			_ENDIF
		_ENDIF

		bit.b	#bBlocked,&masterFlags
		; Warning: the _IF _Z jump below is close to max offset. If necessary, use _IF _NZ ret _ENDIF
		_IF		_Z						; Now not blocked. Can inject commands as needed
			push	&TxBytePtr				; Save current destination for TxByte
			mov		#CmuTxByte,&TxBytePtr	; Switch to CMU output
			_COND
				bit.b	#bTimeout,&masterFlags	; If we recently unblocked via a timeout,
			_AND_IF	_NZ
				bit.b	#bSendZ | bSendi | bSendInit, &masterFlags		; And we have anything to send,
			_AND_IF	_NZ
				mov.b	#$0D,R8					; then send a CR to terminate the stalled command and
												; reset checksums (at receivers)
				call	#TxByte
				clr.b	&txCksum				; Explicitly clear our transmit checksum
				bic.b	#bTimeout,&masterFlags	; Reset the timeout flag
			_ENDIFS
			bit.b	#bSendZ,&masterFlags		; Is a Z command due?
			_IF		_NZ
				mov		#SelectCMU1,R10				; Transmit "1s" to select CMU 1 only
				call	#TxStringCk
				ClearWatchdog

				mov		&discharge+2,Rsec
				mov		#5,Rtos						; 5 digit field width
				push.b	&interpFlags				; Save number base
				bis.b	#bHexOutput,&interpFlags	; Set to hexadecimal output
				call	#_emitNum					; Transmit the number
				popBits_B #bHexOutput,&interpFlags	; Restore number base
				ClearWatchdog

				mov		#'Z',R8						; Transmit a "Z" for ZtoreDischarge
				call	#TxByteCk
				call	#TxEndOfPacket
				bic.b	#bSendZ,&masterFlags		; Don't repeat until needed
			_ENDIF

			_COND
				bit.b	#bSendi,&masterFlags
			_AND_IF		_NZ					; If an 'i' command is due
				bit.b	#bNotSendStatus,&monFlags
			_AND_IF		_Z					; And we're allowing them
				; For the BMU in the Kingscliff DCM, the so-called link-voltage is really shunt current
				; in half amps.
				; This was multiplied by 5 and copied to the variable &current (so tenths of an amp)
				; Tell the CMUs what the current is, by sending an 'i' command
				mov		&current,R10			; Get the saved current, tenths of an amp
				abs		R10						; Take the absolute value of the current in R10
				mov		R10,Rsec
				mov		#4,Rtos					; 4 digit field width
				ClearWatchdog
				call	#_emitNum				; Transmit the number
				ClearWatchdog
				cmp		#0,&current
				_IF		_L						; If current is negative (discharge)
					mov		#'-',R8					; Transmit a postfix minus sign
					call	#TxByteCk
				_ENDIF
				mov		#'i',R8					; Transmit an "i" for current
				call	#TxByteCk
				call	#TxEndOfPacket
				bic.b	#bSendi,&masterFlags	; Don't repeat until needed
			_ENDIFS							; End if 'i' command was due and allowed

			bit.b	#bSendInit,&masterFlags
			_IF		_NZ
				; If we're a BMU, ensure CMUs are echoing commands, using and expecting checksums,
				; and sending status bytes.
				; Then retrieve the high word of the discharge accumulator from CMU 1.
				; The 'k' must go as a separate packet because it must be sent without checksum.
				; The '1sG' must go as a separate packet because it is for a single CMU.
				mov		#'\r',R8				; Send a CR to clear any junk
				call	#TxByte
				mov		#$11,R8					; Send a ctrl-Q (XON) to turn on echo
				call	#TxByte
				mov		#'k',R8					; Transmit "k" with no checksum
				call	#TxByte					;	to tell CMUs to send and expect checksums
				mov		#'\r',R8
				call	#TxByte
#if QUIET
				mov		#Quiet,R10				; Transmit "1Q" to tell CMUs to be quiet
				call	#TxStringCk
				call	#TxEndOfPacket
#endif
				mov		#EnableStatus,R10		; Transmit "0K" to tell CMUs to send status bytes
				call	#TxStringCk
				call	#TxEndOfPacket

				mov		#SelectCMU1Get,R10		; Transmit "1sG" to select CMU 1 only, and get discharge
				call	#TxStringCk
				call	#TxEndOfPacket

				bic.b	#bSendInit,&masterFlags	; Don't repeat
			_ENDIF		; If init due
			pop		&TxBytePtr				; Restore previous TxByte port
		_ENDIF		; If unblocked

		ret

SelectCMU1		DB		2, '1s'			; Length-prefixed command string to select CMU 1
EnableStatus	DB		2, '0K'			; Length-prefixed command string to enable status sending
SelectCMU1Get	DB		3, '1sG'		; Length-prefixed command string to get saved discharge counter
#if QUIET
Quiet			DB		2, '1Q'			; Length-prefixed command string to stop CMUs beeping
#endif
		ALIGN	1

