; Contains both an outer (packet) interpreter with backspace editing
; and a cutdown inner (bytecode) interpreter.
; This bytecode interpreter does not allow nested bytecode definitions,
; i.e. a command character can only be defined in machine code,
; not as a sequence of other command characters.

;-------------------------------------------------------------------------------
;
; Status flag usage
;
; Bit 15 = literal in progress
; Bit 14 = hex literal (otherwise decimal)

; Macros for 3-register interpreter data stack
; Not sure which choice of registers is best
#if 1
	#define 	Rtos	R7		// Top element of data stack
	#define 	Rsec	R4		// Second element from top
	#define 	Rthd	R13		// Third element from top
#else
	#define 	Rtos	R8		// Top element of data stack
	#define 	Rsec	R9		// Second element from top
	#define 	Rthd	R10		// Third element from top
#endif

DPUSH		MACRO	arg
			mov		Rsec,Rthd
			mov		Rtos,Rsec
			mov		arg,Rtos
			ENDM
			
DPUSHb		MACRO	arg
			mov		Rsec,Rthd
			mov		Rtos,Rsec
			mov.b	arg,Rtos
			ENDM
			
DROP		MACRO
			mov		Rsec,Rtos
			mov		Rthd,Rsec
			ENDM
			
TwoDROP		MACRO
			mov		Rthd,Rtos
			ENDM
			
SWAP		MACRO
			xor		Rtos,Rsec
			xor		Rsec,Rtos
			xor		Rtos,Rsec
			ENDM
			
DUP			MACRO
			mov		Rsec,Rthd
			mov		Rtos,Rsec
			ENDM
			
OVER		MACRO
			mov		Rsec,Rthd
			mov		Rtop,Rsec
			mov		Rthd,Rtop
			ENDM

NIP			MACRO
			mov		Rthd,Rsec
			ENDM
			
TUCK		MACRO
			mov		Rtos,Rthd
			ENDM

ROT			MACRO
			xor		Rsec,Rthd
			xor		Rthd,Rsec
			xor		Rsec,Rthd
			xor		Rtos,Rsec
			xor		Rsec,Rtos
			xor		Rtos,Rsec
			ENDM

MinusROT	MACRO
			xor		Rtos,Rsec
			xor		Rsec,Rtos
			xor		Rtos,Rsec
			xor		Rsec,Rthd
			xor		Rthd,Rsec
			xor		Rsec,Rthd
			ENDM

;-------------------------------------------------------------------------------
; Opcode-table Macros

; The opcode lookup table is in high memory.
; It contains one word per bytecode from $60 to $7F, which is the address of the code that implements
; the operation.

_OPCODETBL	EQU		$FFF0-$40				; Immediately below lowest used interrupt vector

; The "x" at the start of the following macro names stands for "cross-compiling"
; to distinguish them from possible native versions on the MSP430 in other versions.
; The name "CODE" is from Forth.


; Assemble an opcode-table entry that has a machine-code definition.

xCODE	MACRO	OPCODE,OPCODENAM,_ADDRLBL
						LSTOUT-
		EVEN								; Force to word boundary
_ADDRLBL:									; Define the machine-code address label
OPCODENAM EQU	OPCODE						; Define the opcode name
		ORG		_OPCODETBL-($60*2)+(OPCODE)*2	; Point to correct opcode-table entry
						LSTOUT+
		DW		_ADDRLBL					; Assemble the address
						LSTOUT-
		ORG		_ADDRLBL					; Point back to where the machine code will be assembled
						LSTOUT+
						ENDM

;-------------------------------------------------------------------------------
; The inner (bytecode) interpreter

; Some bytecodes below $60 need to be translated into the range $60 to $7F
; so they can have an opcode table entry.
		ALIGN	1
translateTable
		DB		'K' , '}'		; Shift-K (for Kill Badness Sending) becomes right curly brace
		DB		'\'', '`'		; Tick (single-quote) (for character literal) becomes tock (back-quote)
		DB		'$' , $7F		; Dollar sign (for hex number literal) becomes <DEL>
		DB		'\\', '|'		; Slosh (backslash) (for comment or EXIT) becomes vertical bar
sizeTranslateTable	EQU	$-translateTable

; Bytecode interpreter for 7 bit bytecodes
_ENTER
_NEXT	
#if WATCHDOG
		mov.w	#WDTPW+WDTCNTCL,&WDTCTL	; Clear and restart Watchdog Timer before each bytecode is interpreted
#endif
		mov.b	@Rip+,Rw	; Fetch the next bytecode
		
		; Do the bytecode translations using the table above
		mov		#sizeTranslateTable,R9 ; Initialise the loop counter and table index
		_BEGIN				; Begin loop
			mov.w	translateTable-2(R9),R10 ; Get two bytes from the table
			cmp.b	R10,Rw		; Compare the bytecode with the low byte from the table
			_WHILE	_NE			; While no match [ _WHILE xx = _IF xx  _CS_SWAP ]
				decd	R9			; Decrement loop-counter/table-index by 2
		_UNTIL	_Z			; Until loop counter is zero
			_ELSE				; Else there is a match so
				swpb	R10			; Get the high byte from the table
				mov.b	R10,Rw		; Substitute it for the bytecode
			_ENDIF				; Endif match

		cmp.b	#$60,Rw		; Treat all bytecodes other than `a-z{|}<DEL>
		jlo		DoUNDEF		;	as undefined, and try to interpret as digits
		
_RUN	rla.b	Rw			; Multiply bytecode by 2
FncOPCD mov _OPCODETBL-($60*2)(Rw),Rw ; Use 2*bytecode to index the opcode-table to get
							;	the code address
		cmp		#$FFFF,Rw	; Check for undefined bytecodes
		jne		FncLIT		; If undefined and >= $60
NotDigit mov	#_Space,Rw	;	Treat as literal-separator no-op

FncLIT	tst		SR			; Check for an implied push-literal
		jge		DoOPCD		; If there's an operand-in-progress
							;	there's an implied LIT now
		bic		#(1<<15)+(1<<14),SR	; Clear the operand-in-progress flag and the hex flag
;		push	Rtos		;	Push the operand now, before doing the
;		mov		Rlit,Rtos	;	operation
		DPUSH	Rlit

DoOPCD	call	Rw			; Call the machine-code implementation of the bytecode
							; It must end with an RET
		jmp		_NEXT

DoUNDEF ; Attempt to interpret undefined bytecodes as hex/decimal digits,
		; otherwise treat them as literal-separating no-ops.
		sub.b	#'0',Rw	; '0'-'9','A'-'F' -> $0-$9,$11-$16
		jn		NotDigit	; Ignore undefined bytecodes below '0'
		cmp.b	#$0A,Rw
		_IF		_GE
			cmp.b	#$17,Rw
			jge		NotDigit ; Ignore undefined bytecodes above 'F'
			cmp.b	#$11,Rw
			jl		NotDigit ; Ignore undefined between '9' and 'A'
			sub.b	#'A'-('9'+1),Rw	; $11-$16 -> $0A-$0F
		_ENDIF
		
FncPFX4 tst		SR			; Test the operand-in-progress flag
		bis		#1<<15,SR	; Set the operand-in-progress flag
		jn		DatMor4		; If there was no operand-in-progress

DatFst4 mov		Rw,Rlit		;	Move 4-bit data to partial-operand register
		jmp		_NEXT		; Else (operand-in-progress)

DatMor4 rla		Rlit		;	Multiply the partial operand by 16 or 10
		mov		Rlit,R9		;	Save 2*Rlit in R9
		rla		Rlit		;	4*Rlit
		rla		Rlit		;	8*Rlit
		bit		#1<<14,SR	; Test the hex flag
		_IF		_Z			; If decimal
			add		R9,Rlit	;	10*Rlit
		_ELSE				; Else hex
			rla		Rlit	;	16*Rlit
		_ENDIF				; Endif
		add		Rw,Rlit		;	Add data into operand reg
DatEnd4 jmp		_NEXT		; Endif


;-------------------------------------------------------------------------------
; The outer (packet) interpreter
; ACCEPT	( char -- )	; Accept the character in R8 into the line buffer.
; Trashes R8 and R9, and more if carriage return is processed
; Implements backspace editing and calls the inner interpreter
; to interpret the line when a valid checksum and a carriage return are received.
ACCEPT
		mov		&ToIN,R9
		_CASE
		
		_OFb	#$0D,R8			; Carriage return = end of packet
			cmp		#TIBEnd,R9
			_IF		_LO				; If packet buffer not full
				bit.b	#bChecksumming,&intFlags
				_IF		_NZ				; If checksumming
					tst.b	&checksum
					_IF		_Z				; If good checksum
						dec		R9				; Prepare to overwrite the checksum with an EXIT bytecode
						cmp		#TIB,R9			; Handle case of empty packet with no checksum
						_IF		_LO
							mov		#TIB,R9
						_ENDIF
				_CS_SWAP				; [ Make next ENDIF apply to _IF _NZ ]
				_ENDIF					; Endif checksumming
						mov.b	#EXIT,0(R9) 	; Overwrite checksum or carriage return with EXIT bytecode
						bit.b	#bDontInterpret,&intFlags
						_IF		_Z				; If the dont-interpret flag is clear
							mov		#TIB,Rip		; Interpret the packet as bytecode
							call	#_ENTER
						_ENDIF					; Endif dont-interpret flag is clear
#if COUNTCKSM
					_ELSE
						inc.b	&worstBadness
						mov.w	&TIB+0,&worstBadMeas
						mov.w	&TIB+2,&worstBadStuf
						mov.w	&TIB+4,&worstBadStuf+2
						mov.w	&TIB+6,&worstBadStuf+4
						mov.w	&TIB+8,&worstBadStuf+6
						mov.w	&TIB+10,&worstBadStuf+8
						mov.w	&TIB+12,&worstBadStuf+10
#endif
					_ENDIF					; Endif good checksum
				_ENDIF					; Endif packet buffer not full
			mov		#TIB,&ToIN		; Initialise packet buffer pointer
			clr.b	&checksum		; Clear the packet checksum
		_ENDOF					; EndOf carriage return
		
		_OFb	#$08,R8			; Backspace editing
			cmp		#TIBEnd,R9
			_IF		_LO				; If packet buffer not full
				dec		R9				; Decrement the pointer, but
				cmp		#TIB,R9			; don't go back past start of packet
				_IF		_LO
					mov		#TIB,R9
				_ENDIF
				mov		R9,&ToIN
			_ENDIF					; Endif packet buffer not full
		_ENDOF					; EndOf backspace
		
		_OFb	#$1B,R8			; Escape to exit eXclusive mode (single BMU interaction)
			bic.b	#bDontInterpret,&intFlags ; Clear the don't interpret flag
		_ENDOF					; EndOf escape
		
		; Default case, if not CR or BS
			cmp		#TIBEnd,R9
			_IF		_LO				; If packet buffer not full
				mov.b	R8,0(R9)		; Write the byte to the buffer
				inc		&ToIN			; Increment the pointer
				xor.b	R8,&checksum	; Accumulate the checksum
			_ENDIF					; Endif packet buffer not full

		_ENDCASE				; End Case char

		ret
		


