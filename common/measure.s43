; Suitable use at appropriate part of assembly file: #include ../common/measure.s43
; Sensitive to these defines:
;	NumSamples

;
; Routine to perform an analogue measurement and put the 12.2 bit result into R8.
; No calibration is performed.
; On input, R8 must contain the measurement channel number shifted left by 12 bits,
; e.g. $7000 for cell voltage, $A000 for temperature, $3000 for link voltage.
; Trashes R9-R10
;
measure:
		bis			R8,&ADC10CTL1			; Choose the desired analog input channel
		bis			#ADC10ON+REFON,&ADC10CTL0 ; Turn on the ADC core, ref and ref buffer
		
		; Wait at least 30 us (= 120 cycles at 4 MHz) for the reference to settle
		_DO			#30,R8					; 30 times around a 4 cycle dec jnz loop
		_LOOP		R8						; Relied on to clear R8 for following

;		mov			#0,R8					; Initialise the sum. R8 cleared by above loop
		_DO			#NumSamples,R9			; Sample counter
			bis			#ENC+ADC10SC,&ADC10CTL0	; Start conversion
			_BEGIN								; Busy wait for conversion to complete
				bit			#ADC10IFG,&ADC10CTL0	; Done conversion?
			_UNTIL		_NZ
			add			&ADC10MEM,R8			; Add a result
		_LOOP		R9
		bic			#ADC10ON+ENC+ADC10SC+REFON,&ADC10CTL0 ; Turn off ADC core, ref and ref buffer
		ret
		