;
; Miscellaneous common functions
;

;
; Transmit the present CRC if error checking is on. Initialise CRC.
;
; This is not in InterruptComms.s43 because it is used by both the Monitor and TestICal
; But the TestICal does not use serialio. It has its own version of TxByteCk.
; Trashes R8 thru R11
TxCksum:
			bit.b		#bErrorChecking,&interpFlags
			_IF		NZ
				mov		&txCksum,R8			; Get the calculated CRC12
				inv		R8					; Invert it
				call	#MakeCrc12Printable	; Convert it to two printable ASCII bytes in R8. Trashes R9
				call 	#TxByte				; Send the LSB of the CRC12. Preserves R8. Trashes R9,10,11
				swpb	R8
				call 	#TxByte				; Send the MSB of the CRC12. Trashes R9,10,11
				_ENDIF
			mov		#InitialCrc12,&txCksum	; Initialise the CRC12
			ret

; Trashes R8 thru R11
TxEndOfPacket:
			call	#TxCksum				; Transmit CRC if required, then initialise CRC.
			mov		#'\r',R8				; Transmit a carriage return.
			br		#TxByte					; Tail-call TxByte and return. Trashes R9,10,11
;			ret

;-------------------------------------------------------------------------------
; TxString	; Transmit counted string pointed to by R10 while not accumulating CRC.
			; Warning: A zero length string will be treated as 256 bytes long.
			; If necessary, wait until the queue is not full.
			; Trashes R8 thru R11.
;-------------------------------------------------------------------------------

TxString: _FOR_B	@R10+,R11
TxBytes:								; Alternative entry point with length in R11
				mov.b	@R10+,R8
				push	R10
				push	R11
				call	#TxByte			; Trashes R9
				pop		R11
				pop		R10
			_NEXT_DEC_B	R11
			ret

;-------------------------------------------------------------------------------
; TxStringCk ; Transmit counted string pointed to by R10 while accumulating CRC.
			; Warning: A zero length string will be treated as 256 bytes long.
			; If necessary, wait until the queue is not full.
			; Trashes R8 thru R11.
;-------------------------------------------------------------------------------

TxStringCk: _FOR_B	@R10+,R11
				mov.b	@R10+,R8
				push	R10
				push	R11
				call	#TxByteCk
				pop		R11
				pop		R10
			_NEXT_DEC_B	R11
			ret

#if defined(MONITOR) || defined(MONOLITH)	// Don't define in TestICal since it won't have
											//	some required definitions (e.g. ChgTxByte)
ChgTxString: _FOR_B	@R10+,R11
ChgTxBytes:								; Alternative entry point with length in R11
				mov.b	@R10+,R8
				push	R10
				push	R11
				call	#ChgTxByte
				pop		R11
				pop		R10
			_NEXT_DEC_B	R11
			ret

ScuTxString: _FOR_B	@R10+,R11
ScuTxBytes:								; Alternative entry point with length in R11
				mov.b	@R10+,R8
				push	R10
				push	R11
				call	#ScuTxByte
				pop		R11
				pop		R10
			_NEXT_DEC_B	R11
			ret

#endif
