;
; Command character definitions for TestICal
;

#include "../common/comDefinitions.s43"			; Include the definitions common to monitor
												; and TestICal

;
; ADCScaled or R(a)wVoltage ( channel -- measurement )
; "channel" has channel number shifted left 12 bits.
; e.g. CellVChan<<12, TempChan<<12, BoltVPlChan<<12, BoltVMiChan<<12.
; "measurement" is 12-bit (0 to 4092) with voltage scale calibration applied,
; which has units of 17/16 of a millivolt in the case of cell and bolt voltages.
;
		xCODE		'a',ADCScaled,_ADCScaled
		mov			Rtos,R8
		call		#MeasAndCorrCell		; Measure and use voltage scale/offset correction
		mov			R10,Rtos
		ret

; Some characters need to be translated into RAM adresses of calibration values
		ALIGN	1
calAddressTable
		DB		'I' , ramBoltMiCal	& $7F | $80
		DB		's' , ramTempSlope	& $7F | $80
		DB		'O' , ramBoltPlOff	& $7F | $00
		DB		'o' , ramCellOff	& $7F | $00
		DB		'c' , ramCapacity	& $7F | $80
		DB		'r' , ramCellRes	& $7F | $80
		DB		'V' , ramBoltPlCal	& $7F | $80
		DB		'v' , ramCellCal	& $7F | $80	; High bit set to indicate word quantity,
		DB		't' , ramTempOff	& $7F | $00	; clear for byte quantity
		DB		'n' , ramBoltMiOff	& $7F | $00
		DB		'i' , ramID			& $7F | $00
		DB		'a' , ramAdcTimIdx	& $7F | $00
; Zero must not appear in the table as it is used to represent "unrecognised".
sizeCalAddressTable	EQU	$-calAddressTable

;
; TranslateToCalAddr ( ch -- addr )
;
TranslateToCalAddr:
		; Do the calibration address translations using the table above
		mov		#sizeCalAddressTable,R9 ; Initialise the loop counter and table index
		_BEGIN				; Begin loop
			mov.w	calAddressTable-2(R9),R10 ; Get two bytes from the table
			cmp.b	R10,Rtos	; Compare the address character with the low byte from the table
			_WHILE	_NE			; While no match [ _WHILE xx = _IF xx  _CS_SWAP ]
				decd	R9			; Decrement loop-counter/table-index by 2
		_UNTIL	_Z			; Until loop counter is zero
		clr		Rtos		; Finished loop with no match so set result to zero for "unrecognised"
			_ELSE				; Else there is a match so
				swpb	R10			; Get the high byte from the table
				mov.b	R10,Rtos	; Substitute it for the address character
			_ENDIF				; Endif match
		ret

;
; Measure and possibly scale by 17/16th
; Needed by 'v, 'b, 'o, 'O calibrate subcommands
; The call to measure trashes R9,10
;
measure17:
		call	#measure	; Get raw 12.2 bit fixed point measurement in R8
		cmp.b	#255,&ID
		_IF		_NE			; If not a BMU then
			; Multiply by 17/16 to change full-scale from 4092 mV to 4348 mV
			mov		R8,R9		; Copy the value
			add		#8,R9		; Add half the divisor for rounding
			rra4	R9			; Divide by 16
			add		R9,R8		; Add back to the original
		_ENDIF
		ret

;
; A worker routine for voltage calibration. We do this twice now for cell/battery and bolt/array
;

; The calibration scale factor will be the expected value divided by the reading.
; The expected value is an unsigned 12 bit integer of millivolts and
; the reading is an unsigned 12.2 bit fixed-point number of millivolts.
; We want the calibration scale factor to be represented as an unsigned 1.15 fixed-point number
; i.e. a number with 1 integer bit and 15 fractional bits.
; So we shift the expected value 17 places left before the division.
; R9 contains the relevant (cell or array/bolt) offset correction as a signed byte
voltScaleWork:
			push	R9
			call	#measure17					; Measure and possibly scale by 17/16; res in R8
			pop		R9							; Restore offset correction
			sxt		R9							; Convert from signed byte to signed word
			add		R9,R8						; Perform offset correction, but not scale correction
			clr		R9							; 16 bits of zeroes
			mov		Rsec,R10
			rla		R10							; 17 zeroes; we have value << 17 in R10:R9
			mov		R8,R11						; The divisor is the voltage reading in R8
			rra		R11							; Add half the divisor to the dividend for rounding
			add		R11,R9
			adc		R10
			call	#UMSlashMod					; Gives quotient in R9, remainder in R10
			; R9 has the value we want to store
			ret

;
; Calibrate ( n ch --- )
; Perform a calibration whose type is ch ('v for voltage etc) and value is n
; For the voltage scale and offset calibrations, we want to apply the other correction (i.e. scale if
; we are calibrating offset, and vice versa) to get the best correction so far. This also allows for
; successive improvement calibration if that turns out to be useful.
;
			xCODE	'c',calibrate,_calibrate
			_CASE
			_OFb	#'v',Rtos					; 'v': Cell voltage scale factor
				mov		#CellVChan<<12,R8			; ADC channel for scaled cell/battery voltage input
				mov.b	&ramCellOff,R9				; This is the offset correction to use
				call	#voltScaleWork
				mov.w	R9,&ramCellCal				; Save cell/battery calibration value
			_ENDOF

			_OFb	#'V',Rtos					; 'b': Bolt+/Array voltage scale factor
				mov		#BoltVPlChan<<12,R8			; ADC channel for scaled bolt/array voltage input
				mov.b	&ramBoltPlOff,R9			; This is the offset correction to use
				call	#voltScaleWork
				mov.w	R9,&ramBoltPlCal			; Save bolt/array calibration value
			_ENDOF

			_OFb	#'o',Rtos					; 'o': Cell voltage offset
				mov		#CellVChan<<12,R8			; ADC channel for scaled cell/battery voltage input
				call	#measure17					; Measure and possibly scale 17/16
				mov		&ramCellCal,R9				; Use this voltage scale for correction
				call	#Fx1_15UMul					; Correct for scale but not offset
				sub		R8,Rsec						; Calculate offset (expected - measured)
				mov.b	Rsec,&ramCellOff
			_ENDOF

			_OFb	#'O',Rtos					; 'O': Bolt+/Array voltage offset
				mov		#BoltVPlChan<<12,R8			; ADC channel for scaled bolt/array voltage input
				call	#measure17					; Measure and possibly scale 17/16
				mov		&ramBoltPlCal,R9			; Use this voltage scale for correction
				call	#Fx1_15UMul					; Correct for scale but not offset
				sub		R8,Rsec						; Calculate offset (expected - measured)
				mov.b	Rsec,&ramBoltPlOff
			_ENDOF

			_OFb	#'t',Rtos					; 't': Temperature offset
				call	#GetTemp					; Get calibrated temp in half degC into R9
				mov.b	&ramTempOff,R8				; Subtract off our existing offset calibration
				sxt		R8
				sub		R8,R9
				push	R9							; Save uncalibrated measurement in half degrees
				; Rsec is supplied in tenths of a degree now. Div by 5 to get half degrees
				mov		#0,R10						; Put Rsec into R10:R9 as dividend
				mov		Rsec,R9
				add		#2,R9						; Add 2 for rounding
				mov		#5,R8						; Divide by 5 to bring to half degrees
				call	#UMSlashMod					; Result in R9
				pop		R8							; Restore uncal'd measurement
				sub		R8,R9						; Subtract measured half degrees from calibration
													; (expected) temperature
				mov.b	R9,&ramTempOff				; Save new temperature offset calibration value
			_ENDOF

			_OFb		#'n',Rtos				; 'n': Bolt-/Shuntx20 voltage offset. Was "link" offset
				mov		#BoltVMiChan<<12,R8
				call	#MeasAndCorrBoltMi			; Measure and correct using the BoltMi corrections
				mov.b	&ramBoltMiOff,R9			; Subtract the existing offset (signed byte)
				sxt		R9
				sub		R9,R10
				sub		#2046,R10					; Zero volts is a 2046 reading

				; Multiply Rsec (signed) by 1.364
				push 	R10
				mov		Rsec,R8
				mov		#44696,R9					; ~= 1.364 * 2^15
				call	#MMStar						; Signed R10:R9 = signed R8 * unsigned R9
				add		#$4000,R9					; Add half the divisor for rounding
				adc		R10
				rla		R9							; To divide by 2^15, mult by 2 and keep the hi word
				rlc		R10
				mov		R10,Rsec
				pop		R10

				sub		Rsec,R10					; Subtract required reading * 1.364
				neg		R10
				_COND								; Start a short-circuit conditional
					cmp		#-$7F,R10					; Compare with -$7F
				_IF_OR		_L						; If < -$7F
					cmp		#$80,R10					; Compare with $80
				_OR_IFS		_GE						; or >= $80
					mov		#$80,R10					; Use $80 to indicate "not calibrated"
				_ENDIF
				mov.b	R10,&ramBoltMiOff			; Save the bolt-/current offset calibration value
			_ENDOF
;
; Default case
				jmp		emitQmark					; Unknown calibration type character

			_ENDCASE

			;jmp		_readCalValue			; Display the value just calibrated and return
												;	(by falling through)
			;
			; Keep 'c' command (above) and 'r' command (below) together
			; That is, don't insert a new command here
			;
;
; Read calibration value ( ch --- )
; Calibration type (e.g. 'v' for cell Voltage) in ch
			xCODE	'r',readCalValue,_readCalValue ; Note: 'r' prints Rx state in monitor
			call 	#TranslateToCalAddr
			mov.b	Rtos,Rsec				; Convert to RAM address in Rsec
			bic.b	#$80,Rsec
			bis		#$200,Rsec
			tst.b	Rtos
			_IF		_NZ						; If not zero (zero means "unrecognised")
;				push.b	&interpFlags
				_IF		_NN						; If high bit clear
					mov		#3,Rtos					; Display 3 digits
					mov.b	@Rsec,Rsec				; Get byte sized cal value
				_ELSE							; Else high bit set so
					mov		#5,Rtos					; Display 5 digits
					cmp		#infoCapacity,Rsec		; Test for battery capacity
					mov.w	@Rsec,Rsec				; Get word sized cal value
;					bis.b	#bHexOutput,&interpFlags ; Display words in hex
					_IF		_EQ
						mov		Rsec,R9
						clr		R10
						mov		#10,R8				; Divide by 10 to bring to whole Ah
						call	#UMSlashMod
						mov		R9,Rsec
					_ENDIF
				_ENDIF							; Endif high bit
			_ELSE 							; Else is zero for "unrecognised"
emitQmark:		mov		#'\\',Rtos				; So question mark will be ignored by downstream CMUs
				call	#_EMIT					; Call EMIT
				mov		#'?',Rtos				; Question mark
				call	#_EMIT					; Call EMIT
				mov		#$0D,Rtos				; Carriage return
				jmp		_EMIT					; Tail-call EMIT and return
			_ENDIF							; Endif unrecognised
			mov.b		#' ',Rthd			; Trailing char (space)
			call	 	#_prettyPrint
;			popBits_B	#bHexOutput,&interpFlags ; Restore output number base
			ret

;
; Write calibration value ( n ch --- )
;
; n is value to write. ch is calibration type (e.g. 'v for cell Voltage).
;
; Note: For the 'rw command, a ballpark estimate of LiFePO4 resistance in microohms would be 144000
; divided by the capacity in amp hours.
; Alternatively cell resistance can be measured as the change in battery voltage
; divided by the step change in current that caused it, divided by the number of cells.
; To obtain "HiTempCellRes", the measured cell resistance should be divided by
; 1 + 2^(1-T/10) where T is the cell temperature at which the measurement was made.
; e.g. at 20 degC, divide the measurement by 1.5 to obtain HiTempCellRes.
; e.g. at 25 degC, divide the measurement by 1.35 to obtain HiTempCellRes.
; e.g. at 30 degC, divide the measurement by 1.25 to obtain HiTempCellRes.

			xCODE	'w',writeCalValue,_writeCalValue
			call 	#TranslateToCalAddr
			mov.b	Rtos,R9					; Convert to RAM address in R9
			bic.b	#$80,R9
			bis		#$200,R9				; Start of RAM
			cmp		#ramCapacity,R9			; Check for battery capacity
			_IF		_EQ						; If so
				rla		Rsec					; Multiply given value by 10
				mov		Rsec,R8					; to bring to tenths of an Ah
				rla2	Rsec					; *8
				add		R8,Rsec					; *10
			_ENDIF
			tst.b	Rtos
			_IF		_NZ						; If not zero for "unrecognised"
				_IF		_NN						; If high bit clear
					mov.b	Rsec,0(R9)				; Set byte sized cal value
				_ELSE							; Else high bit set so
					mov.w	Rsec,0(R9)				; Set word sized cal value
				_ENDIF							; Endif high bit
				cmp	#ram8MHzCalD,R9
				_IF		_EQ
					mov.b	@R9+,&DCOCTL
					mov.b	@R9,&BCSCTL1
				_ENDIF
			_ELSE 							; Else is zero for "unrecognised"
				jmp		emitQmark
			_ENDIF							; Endif unrecognised
			ret


;
; setIDs ( first-ID -- )
;
			xCODE	'i',setIDs,_setIDs
			; When preceded by XOFF (ctrl-S) and the desired first ID in decimal,
			; and followed by <cr> XON (ctrl-Q), it sets consecutive IDs in consecutive CMUs
			; and responds with the ID _after_ the last ID.
			bit.b	#bDontEcho,&interpFlags
			_IF		_NZ					; If the dont-echo flag is set
				; Save our new ID to RAM
				mov.b	Rtos,&ramID

				; Send another setIDs command with its parameter incremented by one.
				inc		Rtos				; Increment the parameter
				DPUSH	#3					; Use a 3 digit field width
				call	#_emitNum			; Transmit the number as text in the current number base

				mov		#'i',R8				; Transmit an "i"
				call	#TxByteCk
				call	#TxEndOfPacket
			_ELSE						; Else dont-echo flag is clear
				; Send '?' to remind user that Ctrl-S needs to be sent before an 'i' command.
				mov		#'?',R8				; Transmit a question mark (a no-op to downstream CMUs)
				call	#TxByte
				mov		#'\r',R8			; Transmit a carriage return. Checksum is deliberately bad.
				call	#TxByte
			_ENDIF
			ret


;
; Update info-flash ( -- )
;
			xCODE	'u',updateInfoFlash,_updateInfoFlash
			; Prepare to flash-program
			; Assumes ACCVIE = NMIIE = OFIE = 0.
			; FSSEL: 1=MCLK FN: 2=divide by 3, 11=divide by 12. Must end up in the range 257-476 kHz

			mov		#FWKEY+FSSEL_1+FN0*(MckPerFTGck-1),&FCTL2 ; Divides MCLK by FN+1
			mov.w	#WDTPW+WDTHOLD,&WDTCTL	; Hold Watchdog Timer

			; Erase appropriate info flash segment.
			mov		#FWKEY,&FCTL3			; Clear LOCK
			mov		#FWKEY+ERASE,&FCTL1		; Enable single segment erase

			rra		&infoDataStart			; Dummy write: Start of cal data in info-flash

			mov		#FWKEY+WRT,&FCTL1		; Enable write (can leave on; only write block that can't)

			clr		R12						; Loop counter
			_BEGIN
				mov.b	ramDataStart(R12),infoDataStart(R12)	; Write a byte to info-flash
				inc		R12
				cmp		#ramDataEnd-ramDataStart,R12
			_UNTIL	_EQ						; Loop until ramDataEnd

			mov		#FWKEY,&FCTL1			; Done. Clear WRT
			mov		#FWKEY+LOCK,&FCTL3		; Set LOCK
			;
			; Check that the flash programming worked
			;
			call	#compareInfoFlash		; Compare RAM cal data with what's in infoflash
			_IF    _NE
				mov	#b'1010000000000000,R8	; 2 flashes
				br		#jErrorFlash		; Infinite loop, never returns
			_ENDIF
			ret

;
; update Bootstrap loader ( -- )
;
			xCODE	'b',updateBSL2,_updateBSL2

			; Avoid an infinite loop if there is a 'b' command in the INIT_CMD_STRING.
			call	#compareBSL2			; Compare the payload copy with the existing BSL2
			_IF		_EQ						; If it's already the same
				ret								; Return without doing anything
			_ENDIF							; Endif

			; Prepare to flash-program
			; Assumes ACCVIE = NMIIE = OFIE = 0.
			; FSSEL: 1=MCLK FN: 2=divide by 3, 11=divide by 12. Must end up in the range 257-476 kHz
			mov		#FWKEY+FSSEL_1+FN0*(MckPerFTGck-1),&FCTL2 ; Divides MCLK by FN+1
			mov.w	#WDTPW+WDTHOLD,&WDTCTL	; Hold the Watchdog Timer

			; Erase last segments of main flash (usually contain BSL2).
			mov		#jBSL,R12				; Start of BSL2 image, $FE00 or $FC00 or ...
			mov		#FWKEY,&FCTL3			; Clear LOCK, don't toggle LOCKA

			push	SR						; Save the interrupt state
			dint							; Disable interrupts while the interrupt vectors are erased
			_BEGIN
				mov		#FWKEY+ERASE,&FCTL1		; Enable single segment erase
				rra		@R12					; Dummy write to erase segment. Takes 16 milliseconds.
				add		#512,R12				; Next segment
			_UNTIL	_NN						; Until the high bit is clear (wraps around to zero)

			mov		#FWKEY+WRT,&FCTL1		; Enable write (can leave on; only write block that can't)
			mov		#jBSL,R12				; Start of BSL2 image
			_BEGIN
				mov		jBSLb-jBSL(R12),0(R12)		; Program word
				incd	R12
			_UNTIL	_NN						; Will wrap to 0 after writing last word
			pop		SR						; Restore the interrupt state

			mov		#FWKEY,&FCTL1			; Done. Clear WRT
			mov		#FWKEY+LOCK,&FCTL3 		; Set LOCK
			;
			; Check that the flash programming worked
			;
			call	#compareBSL2			; Compare the payload copy with what's at $FC00
			_IF		_EQ						; If it was successful
				mov.b	#BSLFG,&IFG1			; Clear the watchdog flag, and indicate a BSL "reset"
				br		#jBSL					; Start the new BSL. Effectively a software reset.
			_ELSE							; Else
				mov	#b'1010100000000000,R8	; 3 flashes
				br		#jErrorFlash		; Infinite loop flashing error LED
			_ENDIF							; Endif
			; Can't ret because then TestICal may return to the wrong place in a changed BSL.


;
; ! StoreWord - store a word (Forth !) ( n addr -- )
;
			xCODE	'!',StoreWord,_StoreWord
			mov.w	Rsec,0(Rtos)
			ret

;
; C! StoreByte - store a byte (C for char) (Forth C!) ( ch addr -- )
;
			xCODE	'C'|'!' <<8,StoreByte,_StoreByte ; 'C!' collides 'Ca' 'Cc' 'Ck' 'Cs' 'C#' 'C+' 'C;'
			mov.b	Rsec,0(Rtos)
			ret

;
; JumpTo - Call the code at addr ( addr -- )
;
			xCODE	'j',JumpTo,_JumpTo
			mov		Rtos,R8		; Save the jump address in a trashable non-stack reg
			DROP				; Drop stack in case the code at addr uses stack arguments
			br		R8			; Tail-call the code at addr and return


;
; f command ( -- )
; This outputs a fixed frequency to TxPl for 5 seconds.
; Used for checking clock calibration.
; Essentially obsolete now that we have a crystal

			xCODE	'f',frequencyBurst,_frequencyBurst
			bic.b	#TxPl,&P1SEL			; Change from UART function to general IO
			bic.b	#TxPl,&P1SEL2			; Both SEL bits must be cleared
			bis.b	#TxPl,&P1DIR			; Set as output in case it isn't already
			mov		#28,R11					; To give 5 seconds with 4 Mhz MCLK
											; Must be even so things toggle back to what they were
			call	#doBurst
			bis.b	#TxPl,&P1SEL			; Back to UART function
			bis.b	#TxPl,&P1SEL2			; No need to change P1DIR
			ret

;
; EMIT ( c -- )	; Emit the character on top of stack
;
		xCODE	'm',EMIT,_EMIT
		mov.b	Rtos,R8
		call	#TxByte
;		pop		Rtos
;		DROP
		ret


;
; Toggle TxPl with a 10 cycle loop,
; This timing depends on TxPl or TxMi being in the low 4 bits of its output register.

; The folowing is irrelevant now that we are using a watch crystal and FLL to achieve 3686400 Hz.

; If we were trying to achieve an exact 4 MHz MCLK, we'd want 4000/2/10 = 200.0 kHz.
; If we were trying to achieve exact 9600 baud comms we'd want 9600*416/2/10 = 199.7 kHz.
; But we've gone with minimising the number of controllers whose default DCO calibration
; we need to change. For that we have settled on the acceptable range being
; 199.5 to 200.5 kHz at 20 degC and
; 200.5 to 201.5 kHz at 30 degC.

doBurst
			mov.w	#WDTPW+WDTHOLD,&WDTCTL	; Hold the Watchdog Timer
			push	SR						; Save interrupt state
			dint							; Disable interrupts (FLL and comms) to guarantee timing
			clr		R10
burstLp		xor.b	#TxPl,&P1OUT 			; (4) Toggle the TxPl bit. Must give 10 cycle loop.
			; We used to toggle both Tx lines in phase but this caused problems with IFO outputs.
			; Leaving one of them as a high output caused problems due to C4 (100n) on the next board.
			; We used to toggle TxMi, but the BMU has it on P3.7.
			; We rely on an even number of toggles to restore TxPl.
			dec		R10						; (1)
			jnz		jBurstLp				; (2) Jump to a 3-cycle delay to burstLp
			dec		R11						;	(alt1 1)
			jnz		burstLp					;	(alt1 2)

			pop		SR						; Restore interrupt state
			ClearWatchdog					; Clear and restart Watchdog Timer

			ret
jBurstLp	br		#burstLp				;	(alt2 3)


;
; send Nulls ( n -- )	; n is the number of nulls to send without a gap
;			Only low byte of n is used. Max of 256 nulls when n is zero.
;			This is to test for false positives on the break-to-reset circuitry of the following CMU.
;
			xCODE	'z',sendNulls,_sendNulls ; 'z' for send Zeros
			mov		#100,R11					; Ensure a proper stop bit
bitDelay	dec		R11
			jnz		bitDelay				; Also clears R11 which is required by BSL WriteByte to save space

			mov.b	#$00,R8					; Null to be sent
			call	#jWriteByte				; Send the null

			ClearWatchdog

			dec.b 	Rtos					; Do it n times, max 256 when n is zero
			jnz		_sendNulls
			ret


;
; Infrared Tx level ( n -- )	; Set the signal level for the infrared emitter, from 0 (off) to 4 (max)
;
		xCODE	'I'|'r' <<8,IrTxLevel,_IrTxLevel ; 'Ir' collides with 'Ib' 'Ij' 'Iz' 'Qr' 'Qb' 'Qj' 'Qz'
			bis.b	#Ir57k6A|Ir57k6B,&P2OUT		; Set both for high output (when not pulsed or open)
			_CASE
				_OF		#0,Rtos
					bic.b	#Ir57k6A|Ir57k6B,&P2SEL		; Both are not pulsed
					bis.b	#Ir57k6A|Ir57k6B,&P2DIR		; Both are outputs (high from above)
				_ENDOF
				_OF		#1,Rtos
					bic.b	#Ir57k6A,&P2SEL				; A is high
					bis.b	#Ir57k6B,&P2SEL				; B is pulsed
					bis.b	#Ir57k6A|Ir57k6B,&P2DIR		; Both are outputs
				_ENDOF
				_OF		#2,Rtos
					bis.b	#Ir57k6A,&P2SEL				; A is pulsed
					bis.b	#Ir57k6A,&P2DIR				; A is an output
					bic.b	#Ir57k6B,&P2SEL				; B is not pulsed
					bic.b	#Ir57k6B,&P2DIR				; B is an input
				_ENDOF
				_OF		#3,Rtos
					bic.b	#Ir57k6A,&P2SEL				; A is not pulsed
					bic.b	#Ir57k6A,&P2DIR				; A is an input
					bis.b	#Ir57k6B,&P2SEL				; B is pulsed
					bis.b	#Ir57k6B,&P2DIR				; B is an output
				_ENDOF
				_OF		#4,Rtos
					bis.b	#Ir57k6A|Ir57k6B,&P2SEL		; Both are pulsed
					bis.b	#Ir57k6A|Ir57k6B,&P2DIR		; Both are outputs
				_ENDOF
			_ENDCASE
			ret

; Some characters need to be translated into info flash adresses of manufacturer's calibration values
		ALIGN	1
ManAddressTable
		DB		'g' , CALADC_GAIN_FACTOR	& $7F | $80	; High bit set to indicate word quantity,
		DB		'o' , CALADC_OFFSET			& $7F | $80	; clear for byte quantity
		DB		'r' , CALADC_15VREF_FACTOR	& $7F | $80
		DB		'3' , CALADC_15T30			& $7F | $80
		DB		'8' , CALADC_15T85			& $7F | $80
; Zero must not appear in the table as it is used to represent "unrecognised".
SizeManAddressTable	EQU	$-ManAddressTable

;
; TranslateToManAddr ( ch -- addr )
;
TranslateToManAddr:
		; Do the calibration address translations using the table above
		mov		#SizeManAddressTable,R9 ; Initialise the loop counter and table index
		_BEGIN				; Begin loop
			mov.w	ManAddressTable-2(R9),R10 ; Get two bytes from the table
			cmp.b	R10,Rtos	; Compare the address character with the low byte from the table
			_WHILE	_NE			; While no match [ _WHILE xx = _IF xx  _CS_SWAP ]
				decd	R9			; Decrement loop-counter/table-index by 2
		_UNTIL	_Z			; Until loop counter is zero
		clr		Rtos		; Finished loop with no match so set result to zero for "unrecognised"
			_ELSE				; Else there is a match so
				swpb	R10			; Get the high byte from the table
				mov.b	R10,Rtos	; Substitute it for the address character
			_ENDIF				; Endif match
		ret

;
; Query manufacturer's calibration value ( ch --- )
; Calibration type (e.g. 'r' for voltage reference calibration value) in ch
			xCODE	'q',queryManValue,_queryManValue ; Note: 'q' queries worst stress in monitor
			call 	#TranslateToManAddr
			mov.b	Rtos,Rsec				; Convert to info flash address in Rsec
			bic.b	#$80,Rsec
			bis		#$1080,Rsec
			tst.b	Rtos
			_IF		_NZ						; If not zero (zero means "unrecognised")
;				push.b	&interpFlags
				_IF		_NN						; If high bit clear
					mov		#3,Rtos					; Display 3 digits
					mov.b	@Rsec,Rsec				; Get byte sized cal value
				_ELSE							; Else high set clear so
					mov		#5,Rtos					; Display 5 digits
					mov.w	@Rsec,Rsec				; Get word sized cal value
;					bis.b	#bHexOutput,&interpFlags ; Display words in hex
				_ENDIF							; Endif high bit
			_ELSE 							; Else is zero for "unrecognised"
				jmp		emitQmark				; Emit a question mark and return
			_ENDIF							; Endif unrecognised
			mov.b		#' ',Rthd			; Trailing char (space)
			call	 	#_prettyPrint
;			popBits_B	#bHexOutput,&interpFlags ; Restore output number base
			ret

;
; Frequency of ADC clock - put 5 s burst out on pin 5, expansion port on CMU, unused on BMU (was TouchV)
		xCODE	'F'|'a' <<8,FreqAdc,_FreqAdc ; 'Fa' collides with 'Fc' 'Fk' 'Fs'
			; Put the ADC10CLK out on processor pin 5 for 5 seconds so it can be measured.
			; On a CMU this connects to the expansion port -- the pin nearest the GND (-) pin.
			; On a BMU this is unconnected (was TouchV analog input).
			call	#GetCellV
			bis.b	#1<<3,P1SEL				; Set ADC10CLK function for pin
			bis.b	#1<<3,P1DIR				; Set as output
			bis		#ADC10ON,&ADC10CTL0		; Turn on the ADC core
			bis		#MSC|ENC|ADC10SC,&ADC10CTL0 ; Enable and start multiple conversions
											; Uses whatever was previously set in ADC10CTL1
			mov		#5000,R8				; Wait 5 seconds
			call	#DelayMs
			bic		#ENC,&ADC10CTL0 		; Must turn off ENC bit before other bits can be changed
			bic		#ADC10ON|MSC,&ADC10CTL0 ; Disable multiple conversions and turn off the ADC core
			bic.b	#1<<3,P1SEL				; Set GPIO function for pin
			bic.b	#1<<3,P1DIR				; Set as input
			ret

